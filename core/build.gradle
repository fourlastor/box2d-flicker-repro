import com.badlogic.gdx.tools.texturepacker.TexturePacker

plugins {
    id("java-library")
    id("idea")
    alias(libs.plugins.spotless)
}

java.sourceCompatibility = JavaVersion.VERSION_1_8
java.targetCompatibility = JavaVersion.VERSION_1_8

ConfigurableFileCollection assetsDir = rootProject.files("assets")

java.sourceSets.main {
    resources.srcDir(assetsDir)
}


spotless {
    enforceCheck = false
    java {
        palantirJavaFormat()
    }
}

def packTextures = tasks.register("packTextures") {
    def inputDir = "$rootDir/assets/images/included"
    def outputDir = "$rootDir/assets/images/packed"
    inputs.dir(inputDir)
    outputs.dir(outputDir)
    doLast {
        delete(outputDir)
        def settings = new TexturePacker.Settings()
        settings.combineSubdirectories = true
        TexturePacker.process(settings, inputDir, outputDir, "images.pack")
    }
}

tasks.compileJava.configure {
    dependsOn(packTextures)
    options.encoding = "UTF-8"
}

dependencies {
    implementation(libs.gdx.core)
    implementation(libs.gdx.box2d.core)
}
